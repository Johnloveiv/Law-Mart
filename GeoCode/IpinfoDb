<!---
	Create an array to hold our IP address locations. The IP Info DB
	can handle up to 25 IP address look-ups at any one time.
--->
<cfset ipAddresses = [] />
 
<!---
	Let's generate 25 random IP addresses (not sure if these will
	all be valid).
--->
<cfloop
	index="i"
	from="1"
	to="25"
	step="1">
 
	<!--- We'll randomly generate the last two octets. --->
	<cfset arrayAppend(
		ipAddresses,
		"71.167.#randRange( 1, 255 )#.#randRange( 1, 255 )#"
		) />
 
</cfloop>
 
<!---
	Loop up the location of all 25 IP address. When we do this, we
	are going to use a different API (notice the "2" at the end of
	the script name).
 
	NOTE: We are explicitly turning OFF the use of timezone since it
	requires the public API to make two more queries on their end.
	We are trying to be kind and let them do as little work as
	needed since we don't need timezone info for the demo.
--->
<cfhttp
	result="ipRequest"
	url="http://ipinfodb.com/ip_query2.php?ip=#arrayToList( ipAddresses )#&timezone=false"
	method="get"
	/>
 
<!--- Check to make sure the result is good. --->
<cfif !reFind( "20\d", ipRequest.statusCode )>
 
	<!--- Something went wrong, so just exit out. --->
	IP Geolocation Failed
	<cfabort/ >
 
</cfif>
 
 
<!---
	If we made it this far, then we we know our IP geolocation
	request has succeeded. Parse the XML into a ColdFusion XML
	document.
--->
<cfset ipInfo = xmlParse( ipRequest.fileContent ) />
 
<!---
	Query the XML document for locations. This will return an
	array of Location XML nodes.
--->
<cfset locations = xmlSearch( ipInfo, "//Location" ) />
 
 
<!--- ----------------------------------------------------- --->
<!--- ----------------------------------------------------- --->
 
 
<cfoutput>
 
	<!DOCTYPE html>
	<html>
	<head>
		<title>Geocoding IP Address using IP Info DB</title>
 
		<style type="text/css">
 
			html,
			body {
				height: 100% ;
				margin: 0px 0px 0px 0px ;
				overflow: hidden ;
				padding: 0px 0px 0px 0px ;
				width: 100% ;
				}
 
			div.mapContainer {
				height: 100% ;
				width: 100% ;
				}
 
		</style>
		<!--- Include jQuery and Google Map scripts. --->
		<script type="text/javascript" src="http://ajax.googleapis.com/ajax/libs/jquery/1.4.2/jquery.min.js"></script>
		<script type="text/javascript" src="http://maps.google.com/maps/api/js?sensor=false"></script>
	</head>
	<body>
 
 
		<div class="mapContainer">
			<!-- This is where Google map will go. --->
		</div>
 
 
		<!---
			Now that we have defined our map container, we should be
			able to immediately load our Google Map.
		--->
		<script type="text/javascript">
 
			// Get the map container node.
			var mapContainer = $( "div.mapContainer" );
 
			// Create the new Goole map controller using our
			// map (pass in the actual DOM object). Center it
			// above the first Geolocated IP address.
			map = new google.maps.Map(
				mapContainer[ 0 ],
				{
					zoom: 9,
					center: new google.maps.LatLng(
						#locations[ 1 ].Latitude.xmlText#,
						#locations[ 1 ].Longitude.xmlText#
					),
					mapTypeId: google.maps.MapTypeId.ROADMAP
				}
			);
 
			// Loop over the geolocated-IP addresses and create
			// markers for each of them.
			<cfloop
				index="location"
				array="#locations#">
 
				new google.maps.Marker({
					map: map,
					position: new google.maps.LatLng(
						#location.Latitude.xmlText#,
						#location.Longitude.xmlText#
					),
					title: "Location #location.xmlAttributes.id#"
				});
 
			</cfloop>
 
		</script>
 
	</body>
	</html>
 
</cfoutput>
